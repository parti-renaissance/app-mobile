apply from: '../config.gradle'
task('getVCode') doLast {
    println "version_code: ${release.versionCode}"
}
task('getVName') doLast {
    if (!project.hasProperty('variant')) {
        println "Specify a flavor via -Pvariant="
        println "Variants available: ${android.applicationVariants.collect { it.name }}"
        return
    }
    def v = android.applicationVariants.findResult({
        it.name.toLowerCase().contains(variant.toLowerCase()) ? it : null
    })
    println "version_name: ${v.versionName}"
}
task('bumpVersionCode') doLast {
    def file = file('../config.gradle')
    def text = file.text
    def newContent = bumpVersionCode(text, 'versionCode')
    def wearContent = bumpVersionCode(newContent, 'wearVersionCode', true)
    file.write(wearContent)
}
private static String bumpVersionCode(String text, String key, boolean silent = false) {
    def matcher = (text =~ /$key: ([0-9]+),/)
    def didMatch = matcher.size() == 1 && matcher.hasGroup()
    if (!didMatch) {
        if (silent) return text
        else throw new FileNotFoundException("Could not find $key in config.gradle")
    }
    def versionCodeStr = matcher[0][1]
    def versionCode = Integer.valueOf(versionCodeStr)
    def newVersionCode = versionCode + 1
    matcher.replaceFirst("$key: " + newVersionCode + ",")
}
